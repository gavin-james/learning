// 针对多模块 打包的策略
buildscript {
    // 自定义参数
    ext {
        // 具体版本依赖可以去 https://github.com/alibaba/spring-cloud-alibaba/wiki/%E7%89%88%E6%9C%AC%E8%AF%B4%E6%98%8E 找相应的版本
        springBootVersion = '2.3.12.RELEASE'
    }

    // 项目仓库设置
    repositories {
        // 优先使用本地 mavene 仓库
        mavenLocal()
        // 阿里云 central 仓和 jcenter 仓的聚合仓
        maven { url 'https://maven.aliyun.com/repository/public/' }
        // 阿里云 gradle 插件仓库
        maven { url 'https://maven.aliyun.com/repository/gradle-plugin/' }
        // 阿里云 spring 代理仓库
        maven { url 'https://maven.aliyun.com/repository/spring/' }
        // 中央maven仓库
        // jcenter()
        mavenCentral()
    }

    // springboot gradle 打包插件依赖
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

//描述
description = 'springboot 学习'

// 所有项目配置
allprojects {
    // 项目信息
    group = 'com.gavin'
    // 版本
    version = '0.0.1-SNAPSHOT'
}

// 子项目设置
subprojects {
    // 签名
    apply plugin: 'signing'
    // java增强
    apply plugin: 'java-library'
    // maven jar 发布
    apply plugin: 'maven-publish'
    // spring依赖管理插件
    apply plugin: 'io.spring.dependency-management'

    // 指定编码格式 第一种方法
    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.warnings = false
    }

    // 项目仓库设置
    repositories {
        // 优先使用本地 mavene 仓库
        mavenLocal()
        // 阿里云 central 仓和 jcenter 仓的聚合仓
        maven { url 'https://maven.aliyun.com/repository/public/' }
        // 阿里云 gradle 插件仓库
        maven { url 'https://maven.aliyun.com/repository/gradle-plugin/' }
        // 阿里云 spring 代理仓库
        maven { url 'https://maven.aliyun.com/repository/spring/' }
        // 中央maven仓库
        jcenter()
        mavenCentral()
    }

    // 依赖范围配置修改
    configurations {
        // 修改annotationProcessor 使其可以继承
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    // 依赖管理
    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
        }
        dependencies {
        }
    }

    // 全局依赖
    dependencies {
        // 注解依赖
        annotationProcessor(
                // lombok 简化开发
                'org.projectlombok:lombok'
        )

        // 测试依赖
        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }
    }

    // 定义您的源文件应该被视为哪种语言版本的 Java。
    sourceCompatibility = JavaVersion.VERSION_1_8
    // 定义您的代码应该运行的最低 JVM 版本，即它确定编译器生成的字节码版本。
    targetCompatibility = JavaVersion.VERSION_1_8

    // 测试配置
    test {
        useJUnitPlatform()
    }
}